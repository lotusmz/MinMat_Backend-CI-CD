plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
    // Para que Gradle use JDK 21 aunque el agente tenga otro JDK
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}

repositories { mavenCentral() }

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // TEST
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperty "spring.profiles.active", "test"
    finalizedBy tasks.jacocoTestReport
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    dependsOn tasks.test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

bootJar {
    archiveBaseName.set('demo-java-spring-api')
    archiveVersion.set('1.0.0')
    archiveFileName.set('demo-java-spring-api-1.0.0.jar')
    launchScript()
}

/** SONARQUBE */
sonarqube {
    properties {
        property "sonar.projectKey", "MindMat"
        property "sonar.projectName", "MindMat"

        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java"
        property "sonar.java.binaries", "build/classes/java/main"
        property "sonar.junit.reportPaths", "build/test-results/test"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"

        // URL y token desde variables de entorno (funciona local y en Jenkins)
        property "sonar.host.url", System.getenv('SONAR_HOST_URL') ?: 'http://sonarqube:9000'
        def token = System.getenv('SONAR_TOKEN') ?: System.getenv('SONAR_AUTH_TOKEN')
        if (token) property "sonar.token", token

        // Opcional: excluir basura de build
        property "sonar.exclusions", "**/build/**,**/.gradle/**"
    }
}

tasks.named('test') {
    ignoreFailures = true
}

